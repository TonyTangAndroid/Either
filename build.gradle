apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'signing'

sourceCompatibility = '1.8'
targetCompatibility = '1.8'
group = 'com.spencerwi'
archivesBaseName = 'Either.java'
version = '2.1.0'

repositories {
    mavenCentral()
}

dependencies {
    testCompile group: 'org.assertj', name:'assertj-core', version:'3.9.1'
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.1.0'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.1.0'
}

test {
    useJUnitPlatform()
}

task javadocJar(type: Jar, dependsOn: javadoc){
    from 'build/docs/javadoc'
    classifier = 'javadoc'
}

task sourcesJar(type: Jar, dependsOn: classes){
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives jar

    archives javadocJar
    archives sourcesJar
}

if (project.hasProperty('isTravis') == false){
    signing {
        sign configurations.archives
    }
    uploadArchives {
        repositories {
            mavenDeployer {
                beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

                repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/"){
                    authentication(userName: sonatypeUsername, password: sonatypePassword)
                }
                snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/"){
                    authentication(userName: sonatypeUsername, password: sonatypePassword)
                }

                pom.project {
                    name 'Either.java'
                    packaging 'jar'
                    description "A right-biased implementation of 'Either a b' for Java, using Java 8 for mapping/folding and type inference."
                    url "http://github.com/spencerwi/Either.java"

                    scm {
                        url 'scm:git@github.com:spencerwi/Either.java.git'
                        connection 'scm:git@github.com:spencerwi/Either.java.git'
                        developerConnection 'scm:git@github.com:spencerwi/Either.java.git'
                    }

                    licenses {
                        license {
                            name 'The MIT License (MIT)'
                            url 'https://raw.githubusercontent.com/spencerwi/Either.java/master/LICENSE'
                            distribution 'repo'
                        }
                    }

                    developers {
                        developer {
                            id 'spencerwi'
                            name 'Spencer Williams'
                        }
                    }

                }
            }
        }
    }
}
